plugins {
  id 'org.gradle.wrapper'
  id 'org.gradle.groovy'
  id 'org.gradle.java-gradle-plugin'
  id 'org.gradle.maven-publish'
  id 'com.fizzpod.sweeney' version '4.0.0'
  id 'com.github.ksoichiro.build.info' version '0.2.0'
  id 'com.gradle.plugin-publish' version '0.10.1'
}

wrapper {
  gradleVersion = '5.0'
}

sweeney {
  enforce type: 'jdk', expect: '[1.8,)'
  enforce 'gradle:[5.0,)'
}

repositories {
  jcenter()
}

dependencies {
  testImplementation "junit:junit:${junitVersion}"
  testImplementation 'org.assertj:assertj-core:3.12.2'
}

test {
  minHeapSize = '128m'
  testLogging.events 'FAILED', 'SKIPPED', 'PASSED'
}

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
  classifier "sources"
}

publishing {
  publications {
    thePlugin(MavenPublication) {
      from components.java
      artifact sourceJar
      pom {
        name = 'EWERK Querydsl Plugin'
        url = "https://github.com/ewerk/querydsl-plugin"
        licenses {
          license {
            name = "The Apache License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
      }
    }
  }
}

gradlePlugin {
  plugins {
    querydslPlugin {
      id = 'com.ewerk.gradle.plugins.querydsl'
      implementationClass = 'com.ewerk.gradle.plugins.querydsl.QuerydslPlugin'
    }
  }
}

pluginBundle {
  website = 'https://github.com/ewerk/querydsl-plugin'
  vcsUrl = 'https://github.com/ewerk/querydsl-plugin'
  description = 'Plugin for generating QueryDSL Q classes.'
  tags = ['querydsl', 'ewerk']

  plugins {
    querydslPlugin {
      displayName = 'Gradle Querydsl plugin'
      description = 'A plugin for generating Querydsl Q classes.'
    }
  }
}
